plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '2.1.21'
}

group = 'org.team11260'
version = '0.1.3'


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    compileOnly 'com.android.tools.build:gradle:8.7.0'
}

publishing {
    publications {
        library(MavenPublication) {
            groupId = 'org.team11260'
            artifactId = 'fast-load-plugin'

            from components.java

            pom {
                name = 'FTC Fast Load Plugin'
                description = 'Allows for hot swapping Team Code without restarting FtcRobotController'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.txt'
                    }
                }
                developers {
                    developer {
                        id = 'matthewo'
                        name = 'Matthew Oates'
                        email = 'uni.oates@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = providers.gradleProperty('matthewo.publish.secret').getOrElse("")
        }
    }
}

gradlePlugin {
    plugins {
        fastload {
            id = 'org.team11260.fast-load-plugin'
            implementationClass = 'org.team11260.FastLoadPlugin'
            displayName = 'FastLoad'
            description = 'Allows for hot swapping Team Code without restarting FtcRobotController'
            tags.set(['ftc', 'java', 'kotlin'])
        }
    }

    website = "https://github.com/Team11260/FtcFastLoadPlugin"
    vcsUrl = "https://github.com/Team11260/FtcFastLoadPlugin"
}
